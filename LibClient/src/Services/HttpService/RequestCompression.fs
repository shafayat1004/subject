module LibClient.Services.HttpService.RequestCompression

open Fable.Core
open Fable.Core.JsInterop
open LibClient.Services.HttpService.HttpService

[<Fable.Core.JS.Pojo>]
type private DeflateAugmentHeadersJs() =
    member val ``X-Content-Encoding`` = "deflate"

[<Fable.Core.JS.Pojo>]
type private CompressedRequestOptionsJs
    ( augmentHeaders: obj, sendData: obj, contentType: string ) =
    member val augmentHeaders = augmentHeaders
    member val sendData = sendData
    member val contentType = contentType

let makeInterceptor (resultUrlRegexSource: string) (minPayloadSizeToEncodeBytes: int) : RequestInterceptor =
    let pako: obj = importDefault "pako"

    let regex = System.Text.RegularExpressions.Regex resultUrlRegexSource

    fun (rawRequestParams: RnRawRequestParams) ->
        let existingOptions =
            match rawRequestParams.MaybeOptions with
            | None         -> createEmpty
            | Some options -> options

        let existingSendData = existingOptions?sendData

        let result =
            if regex.IsMatch rawRequestParams.Url && not (isNullOrUndefined existingSendData) && existingSendData?length >= minPayloadSizeToEncodeBytes then
                let existingAugmentHeaders =
                    if isNullOrUndefined existingOptions?augmentHeaders then
                        createEmpty
                    else
                        existingOptions?augmentHeaders

                let updatedAugmentHeaders =
                    Fable.Core.JS.Constructors.Object.assign (
                        createEmpty,
                        existingAugmentHeaders,
                        DeflateAugmentHeadersJs() |> box
                    )

                let compressedSendData = pako?deflateRaw (existingOptions?sendData)

                let updatedOptions =
                    Fable.Core.JS.Constructors.Object.assign (
                        createEmpty,
                        existingOptions,
                        CompressedRequestOptionsJs(
                            updatedAugmentHeaders,
                            compressedSendData,
                            "application/deflatedJson"
                        ) |> box
                    )

                { rawRequestParams with
                    MaybeOptions = Some updatedOptions
                }

            else
                rawRequestParams

        Async.Of result
