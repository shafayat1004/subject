[<AutoOpen>]
module LibClient.Components.HeaderCell

open Fable.React
open LibClient
open LibClient.Accessibility
open LibClient.Icons
open Rn.Components
open Rn.Styles
open LibClient.JsInterop

module LC =
    module HeaderCell =
        type Theme = {
            FontColor: Color
            FontSize: int
        }

open LC.HeaderCell

[<RequireQualifiedAccess>]
module private Styles =
    let headerCell =
        makeViewStyles {
            FlexDirection.Row
            AlignItems.FlexStart
            JustifyContent.FlexStart
        }

    let headerCellText =
        TextStyles.Memoize(
            fun (theme: Theme) ->
                makeTextStyles {
                    fontSize   18
                    flexShrink 1
                    color      theme.FontColor
                    fontSize   theme.FontSize
                }
        )

    let icon =
        makeViewStyles {
            marginLeft 4
        }

type LibClient.Components.Constructors.LC with
    [<Component>]
    static member HeaderCell<'T when 'T: equality>(
            label: string,
            ?sort: ( (* field *) 'T * (* currentSort *) ('T * SortDirection) * (* setSort *) ('T * SortDirection -> unit)),
            ?numberOfLines: int,
            ?styles: array<ViewStyles>,
            ?theme: Theme -> Theme,
            ?testId: string,
            ?key: string
        ) : ReactElement =
        key |> ignore

        let maybeSort (field: 'T) (currField: 'T) (currDirection: SortDirection) (setSort: 'T * SortDirection -> unit) (_e: ReactEvent.Action) : unit =
            match field = currField with
            | true  -> setSort (field, currDirection.Opposite)
            | false -> setSort (field, SortDirection.Ascending)

        let theTheme = Themes.GetMaybeUpdatedWith theme
        let resolvedTestId =
            testId
            |> Option.orElse (Some (A11ySlug.testId "header-cell" label))
            |> Option.defaultValue "header-cell"
        let numberOfLines =
            match numberOfLines with
            | Some numberOfLines -> Some numberOfLines
            | None -> Undefined

        Rn.View(
            styles =
                [|
                    Styles.headerCell
                    yield! styles |> Option.defaultValue [||]
                |],
            children =
                elements {
                    LC.UiText(
                        label,
                        styles = [| Styles.headerCellText theTheme |],
                        ?numberOfLines = numberOfLines
                    )

                    match sort with
                    | Some (sortField, (currSortField, currSortDirection), setSort) ->
                        if sortField = currSortField then
                            Rn.View(
                                styles = [| Styles.icon |],
                                children =
                                    elements {
                                        let iconCtor =
                                            match currSortDirection with
                                            | SortDirection.Ascending -> Icon.ArrowDown
                                            | _ -> Icon.ArrowUp

                                        iconCtor Color.DevBlue 14
                                    }
                            )

                        LC.Pressable(
                            onPress = maybeSort sortField currSortField currSortDirection setSort,
                            label = label,
                            testId = resolvedTestId,
                            role = AccessibilityRole.Button,
                            overlay = true,
                            componentName = "LC.HeaderCell"
                        )
                    | None ->
                        noElement
                }
        )